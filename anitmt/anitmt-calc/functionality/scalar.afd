///////////////////////////////////////////////////////////////////////////////
/*****************************************************************************/
/*                                                                           */
/*                                scalar functions                           */
/*                                                                           */
/* subfunctions for scalar animations                                        */
/*                                                                           */
/* - scalar_subfunction (abstract base type)                                 */
/* - change (container type for scalars)                                     */
/* - constant                                                                */
/* - linear                                                                  */
/* - accelerated                                                             */
/*                                                                           */
/*****************************************************************************/
///////////////////////////////////////////////////////////////////////////////


// ****************************************************************************
// ****************************************************************************
// ** dependence
// ****************************************************************************
// ****************************************************************************


include declaration "base_func.afd";
include declaration "scalar_base.afd";


// ****************************************************************************
// ****************************************************************************
// ** node definitions
// ****************************************************************************
// ****************************************************************************

// +--------------------+
// | scalar_subfunction |
// +--------------------+

// abstract type for scalar subfunctions 
abstract node scalar_subfunction extends scalar_base provides scalar_vsa {
  
  common {
    actions {
      // MM: the action levels have to be checked !!!
      
      // times, forward and backward
      push( push_time_fw, end_time, scalar_vsa.next.start_time );
      push( push_time_bw, start_time, scalar_vsa.prev.end_time );
      // values, forward and backward
      push( push_state_fw, end_value, scalar_vsa.next.start_value );
      push( push_state_bw, start_value, scalar_vsa.prev.end_value );
      // slopes, forward and backward
      push( push_speed_fw, end_slope, scalar_vsa.next.start_slope );
      push( push_speed_bw, start_slope, scalar_vsa.prev.end_slope );
    }
  }

}

// +--------+
// | change |
// +--------+

// changing values (container for scalar functions)
node change extends scalar_subfunction {
  
  // scalar subfunction provider type 
  contains {
    min1 scalar_vsa ;
  }
  
  common {
    solvers {
      // setting result function parameters
      equal_solver( start_time, scalar_vsa.value(time).start_param );
      equal_solver( end_time  , scalar_vsa.value(time).end_param   );
      equal_solver( start_time, scalar_vsa.slope(time).start_param );
      equal_solver( end_time  , scalar_vsa.slope(time).end_param   );
      equal_solver( start_time, scalar_vsa.acceleration(time).start_param );
      equal_solver( end_time  , scalar_vsa.acceleration(time).end_param   );

      // connection to subfunctions
      equal_solver ( start_time , first_child.start_time  );
      equal_solver ( start_value, first_child.start_value );
      equal_solver ( start_slope, first_child.start_slope );
      equal_solver ( end_time , last_child.end_time  );      
      equal_solver ( end_value, last_child.end_value );      
      equal_solver ( end_slope, last_child.end_slope );      
    }
  }

  // result functions using the scalar subfunctions
  provide scalar_vsa {
    resulting value( time t ) requires child.scalar_vsa.value ( time ) { 
      [[ return child.scalar_vsa.value ( time t ) ]];
    } 
    resulting slope( time t ) requires child.scalar_vsa.slope ( time ) {
      [[ return child.scalar_vsa.slope ( time t ) ]];
    }
    resulting acceleration( time t ) requires child.scalar_vsa.acceleration ( time ) {
      [[ return child.scalar_vsa.acceleration ( time t ) ]];
    }
  }
}

// +----------+
// | constant |
// +----------+

// constant value
node constant extends scalar_subfunction {
  
  // special properties for constant
  properties{
    type scalar {    
      value;
      slope;
      acceleration;
    }
  }
  
  common {
    solvers {
      // setting result function parameters
      equal_solver( start_time, scalar_vsa.value(time).start_param );
      equal_solver( end_time  , scalar_vsa.value(time).end_param   );
      equal_solver( start_time, scalar_vsa.slope(time).start_param );
      equal_solver( end_time  , scalar_vsa.slope(time).end_param   );
      equal_solver( start_time, scalar_vsa.acceleration(time).start_param );
      equal_solver( end_time  , scalar_vsa.acceleration(time).end_param   );

      // solvers for constant
      difference = 0; 
      slope_difference = 0; slope = 0;
      acceleration = 0;
      equal_solver( value, start_value );
      equal_solver( value, end_value );
    }
    
    actions {
      default( default_duration, duration, 0 );
      default( default_state, start_value, 0 );
    }
  }

  // result functions for constant
  provide scalar_vsa {
    resulting value( time t ) requires value
    {
      [[ return_prop value ]];
    }
    resulting slope( time t ) 
    {
      [[ return 0 ]];
    }
    resulting acceleration( time t ) 
    {
      [[ return 0 ]];
    }
  }
}

// +--------+
// | linear |
// +--------+

// linear change of a value
node linear extends scalar_subfunction provides scalar_vsa {

  // special properties for linear
  properties{
    type scalar {    
      slope;
      acceleration;
    }
  }

  common {
    solvers {
      // setting result function parameters
      equal_solver( start_time, scalar_vsa.value(time).start_param );
      equal_solver( end_time  , scalar_vsa.value(time).end_param   );
      equal_solver( start_time, scalar_vsa.slope(time).start_param );
      equal_solver( end_time  , scalar_vsa.slope(time).end_param   );
      equal_solver( start_time, scalar_vsa.acceleration(time).start_param );
      equal_solver( end_time  , scalar_vsa.acceleration(time).end_param   );

      acceleration = 0;
      product_solver( difference, slope, duration );
      equal_solver( slope, start_slope );
      equal_solver( slope, end_slope   );
    }
    actions {
      // MM: the action levels have to be checked

      default( default_speed, slope, 0 );
      default( default_duration, duration, 0 );
      default( default_state, start_value, 0 );
    }
  }

  // result functions for linear
  provide scalar_vsa {
    resulting value( time t ) requires start_value, start_time, slope
    {
      scalar res = [[ start_value ]] + (t - [[ start_time ]]) * [[ slope ]];
      [[ return res ]];
    }
    resulting slope( time t ) requires slope
    {
      [[ return_prop slope ]];
    }
    resulting acceleration( time t ) 
    {
      [[ return 0 ]];
    }
  }
}


// +-------------+
// | accelerated |
// +-------------+

// accelerated change of values
node accelerated extends scalar_subfunction provides scalar_vsa {

  // special properties for accelerated
  properties{
    scalar acceleration;
  }

  common {
    solvers {
      // setting result function parameters
      equal_solver( start_time, scalar_vsa.value(time).start_param );
      equal_solver( end_time  , scalar_vsa.value(time).end_param   );
      equal_solver( start_time, scalar_vsa.slope(time).start_param );
      equal_solver( end_time  , scalar_vsa.slope(time).end_param   );
      equal_solver( start_time, scalar_vsa.acceleration(time).start_param );
      equal_solver( end_time  , scalar_vsa.acceleration(time).end_param   );

      // acceleration solvers
      // still wrong (simple solver):
      accel_solver(difference, duration, acceleration, start_slope, end_slope);
      // correct (better solver): 
      // accel_solver(difference, duration, acceleration, start_slope, end_slope, slope_difference);
    }
    actions {
      // MM: the action levels have to be checked !!! 
      
      default( default_acceleration,  acceleration, 0 ); // <-- still wrong better is:
      // default(200,  slope_difference, 0 );
      default( default_speed, start_slope, 0 );
      default( default_duration, duration, 0 );
      default( default_state, start_value, 0 );
    }
  }

  // result functions for accelerated
  provide scalar_vsa {
    resulting value(time abs_t) 
    requires start_value, start_time, start_slope, acceleration
    {
      scalar t = abs_t - [[ start_time ]];
      scalar res = [[ start_value ]] + [[ start_slope ]]*t + 0.5*[[ acceleration ]]*t*t;
      [[ return res ]];
    }
    resulting slope( time abs_t ) 
    requires start_time, start_slope, acceleration
    {
      scalar t = abs_t - [[ start_time ]];
      scalar res = [[ start_slope ]] + [[ acceleration ]] * t;
      [[ return res ]];
    }
    resulting acceleration( time t ) 
    requires acceleration
    {
      [[ return_prop acceleration ]];
    }
  }
}

// +----+
// | if |
// +----+

// switching between scalar functions (container for then and else)
node if extends scalar_subfunction provides scalar_vsa {
  
  properties {
    // MM: boolean property not finished yet
    type flag {    
      if_condition;
    }
  }
  
  // scalar subfunction provider type 
  contains {
    max1 min1 scalar_then_vsa;
    max1 min1 scalar_else_vsa;
  }
  
  common {
    solvers {
      // setting result function parameters
      equal_solver( start_time, scalar_vsa.value(time).start_param );
      equal_solver( end_time  , scalar_vsa.value(time).end_param   );
      equal_solver( start_time, scalar_vsa.slope(time).start_param );
      equal_solver( end_time  , scalar_vsa.slope(time).end_param   );
      equal_solver( start_time, scalar_vsa.acceleration(time).start_param );
      equal_solver( end_time  , scalar_vsa.acceleration(time).end_param   );
    }

    // special actions connecting only when condition is determined 
    // on a low level with high priority 
    // MM: Have to be implemented 
    actions {
      condition_push (  cond_push_start_time_from_child	, child.scalar_then_vsa.first.start_time        , start_time                                    ,   if_condition  );
      condition_push (  cond_push_start_time_from_child	, child.scalar_else_vsa.first.start_time        , start_time                                    , ! if_condition  );
      condition_push (  cond_push_start_time_to_child	, start_time                                    , child.scalar_then_vsa.first.start_time        ,   if_condition  );
      condition_push (  cond_push_start_time_to_child	, start_time                                    , child.scalar_else_vsa.first.start_time        , ! if_condition  );
      condition_push (  cond_push_end_time_from_child	, child.scalar_then_vsa.last.end_time           , end_time                                      ,   if_condition  );
      condition_push (  cond_push_end_time_from_child	, child.scalar_else_vsa.last.end_time           , end_time                                      , ! if_condition  );
      condition_push (  cond_push_end_time_to_child	, end_time                                      , child.scalar_then_vsa.last.end_time           ,   if_condition  );
      condition_push (  cond_push_end_time_to_child	, end_time                                      , child.scalar_else_vsa.last.end_time           , ! if_condition  );
      condition_push (  cond_push_start_value_from_child, child.scalar_then_vsa.first.start_value       , start_value                                   ,   if_condition  );
      condition_push (  cond_push_start_value_from_child, child.scalar_else_vsa.first.start_value       , start_value                                   , ! if_condition  );
      condition_push (  cond_push_start_value_to_child	, start_value                                   , child.scalar_then_vsa.first.start_value       ,   if_condition  );
      condition_push (  cond_push_start_value_to_child	, start_value                                   , child.scalar_else_vsa.first.start_value       , ! if_condition  );
      condition_push (  cond_push_end_value_from_child	, child.scalar_then_vsa.last.end_value          , end_value                                     ,   if_condition  );
      condition_push (  cond_push_end_value_from_child	, child.scalar_else_vsa.last.end_value          , end_value                                     , ! if_condition  );
      condition_push (  cond_push_end_value_to_child	, end_value                                     , child.scalar_then_vsa.last.end_value          ,   if_condition  );
      condition_push (  cond_push_end_value_to_child	, end_value                                     , child.scalar_else_vsa.last.end_value          , ! if_condition  );
      condition_push (  cond_push_start_slope_from_child, child.scalar_then_vsa.first.start_slope       , start_slope                                   ,   if_condition  );
      condition_push (  cond_push_start_slope_from_child, child.scalar_else_vsa.first.start_slope       , start_slope                                   , ! if_condition  );
      condition_push (  cond_push_start_slope_to_child	, start_slope                                   , child.scalar_then_vsa.first.start_slope       ,   if_condition  );
      condition_push (  cond_push_start_slope_to_child	, start_slope                                   , child.scalar_else_vsa.first.start_slope       , ! if_condition  );
      condition_push (  cond_push_end_slope_from_child	, child.scalar_then_vsa.last.end_slope          , end_slope                                     ,   if_condition  );
      condition_push (  cond_push_end_slope_from_child	, child.scalar_else_vsa.last.end_slope          , end_slope                                     , ! if_condition  );
      condition_push (  cond_push_end_slope_to_child	, end_slope                                     , child.scalar_then_vsa.last.end_slope          ,   if_condition  );
      condition_push (  cond_push_end_slope_to_child	, end_slope                                     , child.scalar_else_vsa.last.end_slope          , ! if_condition  );
/*
      condition_push (  cond_push_start_acceleration_to_child	, child.scalar_then_vsa.first.start_acceleration, start_acceleration                            ,   if_condition  );
      condition_push (  9, child.scalar_else_vsa.first.start_acceleration, start_acceleration                            , ! if_condition  );
      condition_push ( 10, start_acceleration                            , child.scalar_then_vsa.first.start_acceleration,   if_condition  );
      condition_push ( 10, start_acceleration                            , child.scalar_else_vsa.first.start_acceleration, ! if_condition  );
      condition_push ( 11, child.scalar_then_vsa.last.end_acceleration   , end_acceleration                              ,   if_condition  );
      condition_push ( 11, child.scalar_else_vsa.last.end_acceleration   , end_acceleration                              , ! if_condition  );
      condition_push ( 12, end_acceleration                              , child.scalar_then_vsa.last.end_acceleration   ,   if_condition  );
      condition_push ( 12, end_acceleration                              , child.scalar_else_vsa.last.end_acceleration   , ! if_condition  );
*/
    }
  }


  // result functions using the scalar subfunctions
  // MM: Using the boolean type, may not work in the moment
  provide scalar_vsa {
    resulting value ( time t ) 
    requires child.scalar_then_vsa.value ( time ), child.scalar_else_vsa.value ( time ) { 
      scalar res = [[if_condition]] ? [[ child.scalar_then_vsa.value ( time t ) ]] : [[ child.scalar_else_vsa.value ( time t ) ]];
      [[ return res]];
    } 
    resulting slope ( time t ) 
    requires child.scalar_then_vsa.slope ( time ), child.scalar_else_vsa.slope ( time ) { 
      scalar res = [[ if_condition ]] ? [[child.scalar_then_vsa.slope ( time t ) ]] : [[child.scalar_else_vsa.slope ( time t ) ]];
      [[ return res]];
    } 
    resulting acceleration ( time t ) 
    requires child.scalar_then_vsa.acceleration ( time ), child.scalar_else_vsa.acceleration ( time ) { 
      scalar res = [[ if_condition ]] ? [[child.scalar_then_vsa.acceleration ( time t ) ]] : [[child.scalar_else_vsa.acceleration ( time t ) ]];
      [[ return res]];
    } 
  }
}

// +------+
// | then |
// +------+

// then container for if function (container for scalar functions)
node then extends scalar_base provides scalar_then_vsa {
  
  // scalar subfunction provider type 
  contains {
    min1 scalar_vsa;
  }
  
  common {
    solvers {
      // setting result function parameters
      equal_solver( start_time, scalar_then_vsa.value(time).start_param );
      equal_solver( end_time  , scalar_then_vsa.value(time).end_param   );
      equal_solver( start_time, scalar_then_vsa.slope(time).start_param );
      equal_solver( end_time  , scalar_then_vsa.slope(time).end_param   );
      equal_solver( start_time, scalar_then_vsa.acceleration(time).start_param );
      equal_solver( end_time  , scalar_then_vsa.acceleration(time).end_param   );

      // connection to subfunctions (then)
      equal_solver ( start_time , child.scalar_vsa.first.start_time  );
      equal_solver ( start_value, child.scalar_vsa.first.start_value );
      equal_solver ( start_slope, child.scalar_vsa.first.start_slope );
      equal_solver ( end_time   , child.scalar_vsa.last.end_time     );      
      equal_solver ( end_value  , child.scalar_vsa.last.end_value    );      
      equal_solver ( end_slope  , child.scalar_vsa.last.end_slope    );      
    }
  }

  // result functions using the scalar subfunctions
  provide scalar_then_vsa {
    resulting value ( time t ) requires child.scalar_vsa.value ( time ) { 
      [[ return child.scalar_vsa.value ( time t )]];
    } 
    resulting slope ( time t ) requires child.scalar_vsa.slope ( time ) { 
      [[ return child.scalar_vsa.slope ( time t )]];
    } 
    resulting acceleration ( time t ) requires child.scalar_vsa.acceleration ( time ) { 
      [[ return child.scalar_vsa.acceleration ( time t )]];
    } 
  }
}

// +------+
// | else |
// +------+

// else container for if function (container for scalar functions)
node else extends scalar_base provides scalar_else_vsa{
  
  // scalar subfunction provider type 
  contains {
    min1 scalar_vsa;
  }
  
  common {
    solvers {
      // setting result function parameters
      equal_solver( start_time, scalar_else_vsa.value(time).start_param );
      equal_solver( end_time  , scalar_else_vsa.value(time).end_param   );
      equal_solver( start_time, scalar_else_vsa.slope(time).start_param );
      equal_solver( end_time  , scalar_else_vsa.slope(time).end_param   );
      equal_solver( start_time, scalar_else_vsa.acceleration(time).start_param );
      equal_solver( end_time  , scalar_else_vsa.acceleration(time).end_param   );

      // connection to subfunctions (then)
      equal_solver ( start_time , child.scalar_vsa.first.start_time  );
      equal_solver ( start_value, child.scalar_vsa.first.start_value );
      equal_solver ( start_slope, child.scalar_vsa.first.start_slope );
      equal_solver ( end_time   , child.scalar_vsa.last.end_time     );      
      equal_solver ( end_value  , child.scalar_vsa.last.end_value    );      
      equal_solver ( end_slope  , child.scalar_vsa.last.end_slope    );      
    }
  }

  // result functions using the scalar subfunctions
  provide scalar_else_vsa {
    resulting value ( time t ) requires child.scalar_vsa.value ( time ) { 
      [[ return child.scalar_vsa.value ( time t )]];
    } 
    resulting slope ( time t ) requires child.scalar_vsa.slope ( time ) { 
      [[ return child.scalar_vsa.slope ( time t )]];
    } 
    resulting acceleration ( time t ) requires child.scalar_vsa.acceleration ( time ) { 
      [[ return child.scalar_vsa.acceleration ( time t )]];
    } 
  }
}


// +------+
// | test |
// +------+

// switching between scalar functions (container for then and else)
node test extends scalar_subfunction provides scalar_vsa {
  
  properties {
    type scalar {
      ret_value;
      ret_slope;
      ret_acceleration;
    }
    type flag {
      f1;f2;f3;f4;f5;f6;f7;f8;f9;f10;
      f11;f12;f13;f14;f15;f16;f17;f18;f19;f20;
    }
    type scalar {
      s1;s2;s3;s4;s5;s6;s7;s8;s9;
    }
    type vector {
      v1;v2;v3;v4;v5;v6;v7;v8;v9;
    }
    type matrix {
      m1;m2;m3;m4;m5;m6;m7;m8;m9;
    }
    type string {
      str1;str2;str3;str4;str5;str6;str7;str8;str9;
    }
  }
  
  common {
    solvers {
      f1 = true;
      s1 = 1;
      v1 = to_vector( 1,2,3 );
      m1 = mat_rotate_x( 3.141 / 3 );
      str1 = "string";
      f2 = f1 == true;			// true
      f3 = f2 == false;			// false
      f4 = s1 == 1;			// true
      f5 = s1 == 2;			// false
      f6 = v1 == to_vector( s1,2,3 );	// true
      f7 = v1 == to_vector( 1,s1,3 );	// false
      f8 = m1 == mat_rotate_x( 3.141000000001 / 3 );// true
      f9 = m1 == mat_rotate_y( 3.141 / 3 );	// false
      s2 = get_element( v1, 2 );	// 3
      s3 = get_element( m1, 1, 1 );	// m[1][1]
      v2 = get_rotate_component( m1 ); // <pi/3,0,0>
      m2 = mat_translate_x(3) * mat_rotate_z(2) * mat_scale_y(4) * m1;
      v3 = m2 * to_vector(1,0,0);
      v4 = m2 * to_vector(0,1,0);
      v5 = m2 * to_vector(0,0,1);
      v6 = get_rotate_component( m2 );
      v7 = get_translate_component( m2 );
      v8 = get_scale_component( m2 );
      m3 = mat_translate_z( get_element(v7,2) ) * 
           mat_translate_y( get_element(v7,1) ) *
           mat_translate_x( get_element(v7,0) ) *
           mat_rotate_z( get_element(v6,2) ) * 
           mat_rotate_y( get_element(v6,1) ) *
           mat_rotate_x( get_element(v6,0) ) *
           mat_scale_z( get_element(v8,2) ) * 
           mat_scale_y( get_element(v8,1) ) *
           mat_scale_x( get_element(v8,0) ); // m3 = m2
      m4 = mat_translate( v7 ) * 
           mat_rotate( v6 ) * 
           mat_scale( v8 );		// m4 = m3 = m2
      f10 = m2 == m3 || m3 == m4;	// true
      f11 = !f10;			// false
      f12 = !s1;			// false
      f13 = !v1;			// false
      f14 = !m1;			// false
      f15 = !s1;			// false
      f16 = !false;			// true
      f17 = !0;				// true
      f18 = !to_vector(0,0,0);		// true
      f19 = !mat_translate_x(0);		// true
      f20 = !"";			// true
      v9 = vec_translate( to_vector(1,2,3), 5, 2 );
					// <6,6,6>
    }
  }

  // result functions using the scalar subfunctions
  // MM: Using the boolean type, may not work in the moment
  provide scalar_vsa {
    resulting value ( time t ) requires ret_value { 
      [[ return_prop ret_value]];
    } 
    resulting slope ( time t ) requires ret_slope { 
      [[ return_prop ret_slope]];
    } 
    resulting acceleration ( time t ) requires ret_acceleration { 
      [[ return_prop ret_acceleration]];
    } 
  }
}
